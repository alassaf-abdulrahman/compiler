<program>           ::= <command_list>

<command_list>      ::= <simple_command> ";" <command_list>
                      | <control_command> <command_list>
                      | Îµ

<simple_command>     ::= <basic_command> <scheduled_command>?

<basic_command>      ::= <open_command>
                      | <close_command>
                      | <turnon_command>
                      | <turnoff_command>

<control_command>    ::= <if_command>

<open_command>      ::= "OPEN" <open_task>
<close_command>     ::= "CLOSE" <open_task>
<turnon_command>    ::= "TURNON" <turn_task>
<turnoff_command>   ::= "TURNOFF" <turn_task>

<scheduled_command> ::= "AT" <time>
                      | "IN" <duration>

<if_command>        ::= "IF" <condition> "{" <command_list> "}" <else_block>?

<else_block>        ::= "ELSE" "{" <command_list> "}"

<condition>         ::= <open_task> "IS" <open_state>
                      | <turn_task> "IS" <turn_state>

<open_task>         ::= "DOOR" | "WINDOW"
<turn_task>         ::= "LIGHT" | "WIFI" | "AIRCOND"

<open_state>        ::= "OPENED" | "CLOSED"
<turn_state>        ::= "ON" | "OFF"

<time>              ::= <hour1> <digit> <minute1> <digit>
<duration>          ::= <minutes> "M"

<hour1>             ::= "0" | "1" | "2"
<minute1>           ::= "0" | "1" | "2" | "3" | "4" | "5"
<minutes>           ::= <non_zero_digit> <digit>*
                      | "0"
<digit>             ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<non_zero_digit>    ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"